/*******************************************************************************
*
* McXtrace, xray-tracing package
*         Copyright (C) 1997-2016, All rights reserved
*         Dtu Physics, Kgs. Lyngby, Denmark

* Component: MCPL_output
*
* %I
* Written by: Erik B Knudsen 
* Date: Mar 2016
* Version: $Revision$
* Origin: DTU Physics
* Release: McXtrace 1.3
*
* Detector-like component that writes photon state parameters into an mcpl-format 
* binary photon file.
*
* %D
* long description
* %BUGS
*
* %P
* INPUT PARAMETERS
*
* filename: [str] Name of photon file to write. Default is standard
*                 output [string]. If not given, a unique name will be used.
*
* %E
*******************************************************************************/

DEFINE COMPONENT MCPL_output
DEFINITION PARAMETERS (polarisationuse=1, doubleprec=1, dump_file=0)
SETTING PARAMETERS (string filename=0)
OUTPUT PARAMETERS (outputfile)
DEPENDENCY "-L. -lmcpl"

SHARE
%{
#include "mcpl.h"
%}

DECLARE
%{
    mcpl_outfile_t outputfile;
%}

INITIALIZE
%{
    char line[256];
    outputfile = mcpl_create_outfile(filename);
    snprintf(line,255,"%s %s %s",MCCODE_NAME,MCCODE_VERSION,mcinstrument_name);
    mcpl_hdr_set_srcname(outputfile,line);
    mcpl_enable_universal_pdgcode(outputfile,22);//all particles are photons
    snprintf(line,255,"Output by COMPONENT: %s",NAME_CURRENT_COMP);
    mcpl_hdr_add_comment(outputfile,line);

    if (polarisationuse) {
        mcpl_enable_polarisation(outputfile);
    }
    if (doubleprec){
        mcpl_enable_doubleprec(outputfile);
    }

%}

TRACE
%{
    double nrm;
    mcpl_particle_t *particle = (mcpl_particle_t *) calloc(sizeof(mcpl_particle_t),1);
    particle->position[0]=x;
    particle->position[1]=y;
    particle->position[2]=z;

    if(polarisationuse){
        particle->polarisation[0]=Ex;
        particle->polarisation[1]=Ey;
        particle->polarisation[2]=Ez;
    }

    nrm =sqrt(kx*kx + ky*ky + kz*kz);
    /*ekin is in MeV:*/
    particle->ekin = 1e3*K2E*nrm;
    particle->direction[0] = kx/nrm;
    particle->direction[1] = ky/nrm;
    particle->direction[2] = kz/nrm;
    /*time in ms:*/
    particle->time = t*1e3;
    /*weight in unspecified units:*/
    particle->weight = p;

    mcpl_add_particle(outputfile,particle);
    
    SCATTER;
%}

SAVE
%{
    mcpl_close_outfile(outputfile);  
%}

FINALLY
%{
    if (dump_file) mcpl_dump(filename,0,0,(int) dump_file);
%}

MCDISPLAY
%{
    double t,dt;
    int i;
    multiline(5,0.2,0.2,0.0, -0.2,0.2,0.0, -0.2,-0.2,0.0, 0.2,-0.2,0.0, 0.2,0.2,0.0);
    /*M*/
    multiline(5,-0.085,-0.085,0, -0.085,0.085,0, -0.045,-0.085,0, -0.005,0.085,0, -0.005,-0.085,0);
    /*O*/
    dt=2*M_PI/32;
    t=0;
    for (i=0;i<32;i++){
        line(0.04*cos(t)+0.045,0.04*sin(t),0, 0.04*cos(t+dt)+0.045,0.04*sin(t+dt),0);
        t+=dt;
    }
%}

END

